apply plugin:'groovy'
apply plugin:'eclipse'

repositories {
    mavenCentral()
    mavenRepo urls:["http://download.java.net/maven/1",
        "http://download.java.net/maven/2"]
}

configurations {
    jaxws
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDir 'src' }
    }
    test {
        java { srcDirs = [] }
        groovy { srcDir 'test' }
        resources { srcDir 'resources' }
    }
}

task wsimport(dependsOn: processResources)  {
    doLast{
        sourceSets.main.classesDir.mkdirs()
        ant {
            taskdef(name:'wsimport',
                classname:'com.sun.tools.ws.ant.WsImport',
                classpath:configurations.jaxws.asPath)
            wsimport(keep:true,
                destdir: sourceSets.main.classesDir,
                sourcedestdir:'src',
            wsdl:'http://localhost:1234/calc?wsdl')
        }
    }
}
wsimport.onlyIf { !(new File('src/mjg/calc/service')).exists() }
compileJava.dependsOn(wsimport)

def groovyVersion = '1.8.4'
def spockVersion = '0.5-groovy-1.8'

dependencies {
    groovy "org.codehaus.groovy:groovy-all:$groovyVersion"
    jaxws 'com.sun.xml.ws:jaxws-tools:2.1.4'

    testCompile "org.spockframework:spock-core:$spockVersion"
}
