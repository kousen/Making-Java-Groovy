apply plugin:'groovy'
apply plugin:'war'
apply plugin:'eclipse'

repositories {
    mavenCentral()
}

db = file("$buildDir/baseball.h2.db")

task generateTestDb(dependsOn:classes, type:JavaExec) {
    onlyIf { !db.exists() }
    main = 'service.populate_stadium_data'
    classpath sourceSets.main.runtimeClasspath
}

task deployDb(dependsOn:generateTestDb, type:Copy) {
    from db
    into 'src/main/webapp/build'
}

war.dependsOn deployDb

task removeDb(type:Delete) {
    delete 'src/main/webapp/build'
}

clean.dependsOn removeDb

task wrapper(type:Wrapper) {
    gradleVersion = '1.0-milestone-5'
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:1.8.4'

    testCompile 'junit:junit:4.8.2'
    testCompile 'org.spockframework:spock-core:0.5-groovy-1.8'
    testRuntime "cglib:cglib-nodep:2.2"
    runtime 'com.h2database:h2:1.2.140'
}
